/*
 * Copyright 2019 Amazon.com, Inc. or its affiliates.  All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the 'License').
 *  You may not use this file except in compliance with the License.
 * A copy of the License is located at:
 *
 *      http://aws.amazon.com/apache2.0/
 *
 *  or in the 'license' file accompanying this file. This file is distributed on an 'AS IS' BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific
 *  language governing permissions and limitations under the License.
 */

// Documentation for using Gradle with Kotlin is here: https://kotlinlang.org/docs/reference/using-gradle.html

plugins {
    // https://docs.gradle.org/current/userguide/java_library_plugin.html#header
    id 'java-library'
    // https://docs.gradle.org/5.0/userguide/publishing_maven.html#header
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'io.gitlab.arturbosch.detekt'

    id "signing"
}



tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    // run-time dependencies
    // TODO:  don't expose Kotlin stdlib as part of the API.  This is currently needed because parts of the
    // interpreter's API use kotlin.sequences.Sequence<ExprValue>.  Once that has been removed, we can change
    // 'api' below to 'implementation' to effect this.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    api 'com.amazon.ion:ion-java:[1.4.0,)'

    // test-time dependencies
    testImplementation 'org.jetbrains.kotlin:kotlin-reflect'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'pl.pragmatists:JUnitParams:[1.0.0,1.1.0)'
    testImplementation 'org.assertj:assertj-core:[3.11.0,3.12.0)'
    
    // PTS
    testImplementation project(':testscript')
}

tasks.register('sourcesJar', Jar) {
    from(sourceSets.main.allJava)
    classifier = 'sources'
}

tasks.register('javadocJar', Jar) {
    from(tasks.javadoc)
    classifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            // group id and version are set in the root build.gradle for all projects.
            artifactId = "partiql-lang-kotlin"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "PartiQL-Lang-Kotlin"
                packaging = "jar"
                url = "https://github.com/amzn/ion-schema-kotlin"
                description = "An implementation of PartiQL for the JVM written in Kotlin."
                scm {
                    connection = "scm:git@github.com:partiql/partiql-lang-kotlin.git"
                    developerConnection = "scm:git@github.com:partiql/partiql-lang-kotlin.git"
                    url = "git@github.com:partiql/partiql-lang-kotlin.git"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "Amazon Ion Team"
                        email = "partiql-community@amazon.com"
                        organization = "PartiQL"
                        organizationUrl = "https://github.com/partiql"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

// The below enables developers and CI servers who do not have the GPG keys needed for signing to build locally,
// while also signing release builds.
// Signatures are only required when publishing to maven central anyway. We may need a more sophisticated means of
// determining if we should sign the build artifacts someday, but this should suffice for now.
if(!version.endsWith("SNAPSHOT")) {
    println("""
*******************************************************************************
Note: 

The build artifacts will be signed because this is NOT a SNAPSHOT version. 
Signing will fail if you have not configured a ~/.gradle/gradle.properties 
file with the correct values.  
*******************************************************************************
""")

    signing {
        sign publishing.publications.maven
    }
}


