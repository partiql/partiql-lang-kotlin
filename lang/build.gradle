
import org.partiql.pig.cmdline.Command
import org.partiql.pig.cmdline.TargetLanguage
import org.partiql.pig.errors.PigException

/*
 * Copyright 2019 Amazon.com, Inc. or its affiliates.  All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the 'License').
 *  You may not use this file except in compliance with the License.
 * A copy of the License is located at:
 *
 *      http://aws.amazon.com/apache2.0/
 *
 *  or in the 'license' file accompanying this file. This file is distributed on an 'AS IS' BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific
 *  language governing permissions and limitations under the License.
 */

// Documentation for using Gradle with Kotlin is here: https://kotlinlang.org/docs/reference/using-gradle.html

plugins {
    // https://docs.gradle.org/current/userguide/java_library_plugin.html#header
    id 'java-library'
    // https://docs.gradle.org/5.0/userguide/publishing_maven.html#header
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'org.jetbrains.kotlin.jvm'
    id 'signing'
    id 'me.champeau.gradle.jmh' version '0.5.3'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    // run-time dependencies
    // TODO:  don't expose Kotlin stdlib as part of the API.  This is currently needed because parts of the
    // interpreter's API use kotlin.sequences.Sequence<ExprValue>.  Once that has been removed, we can change
    // 'api' below to 'implementation' to effect this.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    api 'com.amazon.ion:ion-java:[1.4.0,)'
    api 'com.amazon.ion:ion-element:0.2.0'
    api 'org.partiql:partiql-ir-generator-runtime:0.3.2'

    // test-time dependencies
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'pl.pragmatists:JUnitParams:[1.0.0,1.1.0)'
    testImplementation 'org.assertj:assertj-core:[3.11.0,3.12.0)'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
}

sourceSets {
    jmh.kotlin.srcDirs = ["jmh"]
}

task generatePigDomains {
    group = "code generation"

    // TODO: in the future, utilize the API created as part of this ticket:
    // https://github.com/partiql/partiql-ir-generator/issues/5

    def typeUniverse = new File(projectDir, "resources/org/partiql/type-domains/partiql.ion")
    def outputFile = new File(projectDir, "src/org/partiql/lang/domains/partiql-domains.kt")
    def targetLanguage = new TargetLanguage.Kotlin("org.partiql.lang.domains")
    def cmd = new Command.Generate(typeUniverse, outputFile, targetLanguage)

    // TODO:
    // Tell gradle about the input and output of this task so that it only invokes pig when
    // the input file has changed or the output file is missing.

    doLast {
        try {
            org.partiql.pig.MainKt.generateCode(cmd)
        } catch(PigException e) {
            System.err.println("Failed to generate PIG domains: ${e.error}")
            throw new TaskExecutionException(generatePigDomains, e)
        }
    }
}

compileKotlin.dependsOn generatePigDomains


dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/javadoc"
    //todo: includes = ["path/to/module.md"]
}

task sourcesJar(type: Jar) {
    from "src"
    classifier = "sources"
}

task javadocJar(type: Jar) {
    from dokka
    classifier = "javadoc"
}

publishing {
    publications {
        maven(MavenPublication) {
            // group id and version are set in the root build.gradle for all projects.
            artifactId = "partiql-lang-kotlin"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "PartiQL Lang Kotlin"
                packaging = "jar"
                url = "https://partiql.org/"
                description = "An implementation of PartiQL for the JVM written in Kotlin."
                scm {
                    connection = "scm:git@github.com:partiql/partiql-lang-kotlin.git"
                    developerConnection = "scm:git@github.com:partiql/partiql-lang-kotlin.git"
                    url = "git@github.com:partiql/partiql-lang-kotlin.git"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "Amazon Ion Team"
                        email = "ion-team@amazon.com"
                        organization = "PartiQL"
                        organizationUrl = "https://github.com/partiql"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://aws.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    // Allow publishing to maven local even if we don't have the signing keys
    // This works because when not required, the signing task will be skipped
    // if signing.keyId, signing.password, signing.secretKeyRingFile, etc are
    // not present in gradle.properties.
    required { !gradle.taskGraph.hasTask(":lang:publishToMavenLocal") }

    sign publishing.publications.maven
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}