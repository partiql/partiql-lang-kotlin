plugins {
    id 'application'
}

application {
    applicationName = 'partiql' // startup script name
    mainClass.set("org.partiql.cli.Main")
}

// distribution configuration, brought in by `application` plugin
distributions {
    main {
        distributionBaseName.set('partiql-cli') // archive name
        contents {
            from 'archive'  // include contents of archive dir into the final zip/tar
        }
    }
}

(tasks.findByName("distTar") as Tar).compression = Compression.GZIP

// Allows the CLI to receive input from the keyboard when invoked using `./gradlew :cli:run`
tasks['run'].standardInput = System.in

dependencies {
    implementation project(":lang")
    implementation 'net.sf.jopt-simple:jopt-simple:[5.0,6.0)'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation 'org.apache.commons:commons-csv:1.8'

    testImplementation 'org.junit.vintage:junit-vintage-engine:5.7.0'
}
