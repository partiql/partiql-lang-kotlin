plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'application'
    // https://arturbosch.github.io/detekt/groovydsl.html
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC16"
}

version = "0.1.0"

application {
    mainClassName = "org.partiql.cli.Main"
    applicationName = 'partiql' // startup script name
}

// distribution configuration, brought in by `application` plugin
distributions {
    main {
        baseName = 'partiql-cli' // archive name
        contents {
            from 'archive'  // include contents of archive dir into the final zip/tar
        }
    }
}





(tasks.findByName("distTar") as Tar).compression = Compression.GZIP

// Allows the CLI to receive input from the keyboard when invoked using `./gradlew :cli:run`
tasks['run'].standardInput = System.in


dependencies {
    implementation project(":lang")
    implementation 'net.sf.jopt-simple:jopt-simple:[5.0,6.0)'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'pl.pragmatists:JUnitParams:[1.0.0,1.1.0)'
}


detekt {
    input = files("src/")
    parallel = true                                       // Builds the AST in parallel. Rules are always executed in parallel. Can lead to speedups in larger projects. `false` by default.
    debug = false                                         // Adds debug output during task execution. `false` by default.
    ignoreFailures = true                                 // If set to `true` the build does not fail when the maxIssues count was reached. Defaults to `false`.
    reports {
        html {
            enabled = true                                // Enable/Disable HTML report (default: true)
            destination = file("build/reports/detekt/detekt.html") // Path where HTML report will be stored (default: `build/reports/detekt/detekt.html`)
        }
    }
}