plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'application'
}

version = "1.0.0"

application {
    mainClassName = "org.partiql.cli.Main"
}

(tasks.findByName("distTar") as Tar).compression = Compression.GZIP

// Allows the CLI to receive input from the keyboard when invoked using `./gradlew :cli:run`
tasks['run'].standardInput = System.in

jar {
    dependsOn ':lang:jar'
    
    archiveName = 'partiql-cli.jar'
    
    manifest {
        attributes("Implementation-Title": project.name,
                "Implementation-Version": version,
                "Main-Class": "org.partiql.cli.Main")
    }
    
    // includes all runtime dependencies in the jar
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

task packageCli(type: Zip) {
    dependsOn ':cli:jar'
    
    from ('archive') {
        include '*' 
    }
    
    from('build/libs') {
        include '*.jar'
    }
    
    baseName = 'partiql-cli'
}

dependencies {
    implementation project(":lang")
    implementation 'net.sf.jopt-simple:jopt-simple:[5.0,6.0)'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'pl.pragmatists:JUnitParams:[1.0.0,1.1.0)'
}
